*Reference: copy and paste the original replication code

global rawdata C:\Users\fanga\Desktop\Econ Materials\STATA\replications in stata\AirPollu-criminal-rep\Raw-Data
global data C:\Users\fanga\Desktop\Econ Materials\STATA\replications in stata\AirPollu-criminal-rep\Data
clear all
*this set up will be permanently changed in stata
set mem 1000m
set matsize 2000

//Step1: Append Data//
insheet using "$rawdata/no2_chicago_20000101_20041231.txt", comma clear
drop qualifierdescription
save "$data/temp.dta", replace
insheet using "$rawdata/no2_chicago_20050101_20091231.txt", comma clear
drop qualifierdescription
save "$data/temp2.dta", replace
insheet using "$rawdata/no2_chicago_20100101_20121231.txt", comma clear
drop qualifierdescription
append using "$data/temp.dta"
append using "$data/temp2.dta"

drop if latitude == "END OF FILE"
destring latitude longitude, replace

gen str monitorid = string(county)+"_"+string(sitenum)+"_"+string(poc)

//Step2: Generate Hourly Data//
preserve
tab monitorid sampleduration

* 8 HOUR RUNNING AVERAGE IS SOMETHING I CAN RE-CALCULATE MYSELF
* THERE ARE ALMOST NO OBSERVATIONS WITH AN 8 HOUR AND NO 1 HOUR
keep if sampleduration == "1 HOUR"
gen observed = !mi(samplemeasurement)
bys monitorid datelocal: egen num_hrly_obs = sum(observed)

* KEEP DAYS WITH >= 18 OBSERVATIONS
keep if num_hrly_obs >= 18

* GENERATE DATE AND HOUR VARIABLES
gen date = date(datelocal,"YMD",2015)
format date %td
gen hour = substr(hourlocal,1,2)
destring hour, replace

rename samplemeasurement no2_hrly
replace no2_hrly = no2_hrly/1000
label var no2_hrly "Hourly concentration NO2, ppm"

keep latitude longitude datum statecode countycode monitorid sitenum poc no2_hrly date hour
compress

notes: NO2 monitors in Chicago CBSA
notes: Keep only monitor-days with 18 or more hourly observations
notes: Generated by extract_chicago_no2_2000_2012.do
save "$data/chicago_no2_2000_2012_hourly.dta", replace
restore

//Step3: Generate Daily Data//
* GENERATE AVERAGE HOUR, MAX HOUR
gen observed = !mi(samplemeasurement)
drop if sampleduration == "8-HR RUN AVG BEGIN HOUR"

bys monitorid datelocal sampleduration: egen num_hrly_obs_no2 = sum(observed)
bys monitorid datelocal sampleduration: egen max_no2 = max(samplemeasurement)
bys monitorid datelocal sampleduration: egen avg_no2 = mean(samplemeasurement)

replace max_no2 = max_no2/1000
replace avg_no2 = avg_no2/1000
label var max_no2 "Day's max hourly concentration NO2, ppm"
label var avg_no2 "Day's avg hourly concentration NO2, ppm"

* GENERATE DATE VARIABLE
gen date = date(datelocal,"YMD",2015)
format date %td

egen tag = tag(monitorid date)
keep if tag == 1
drop tag

* KEEP DAYS WITH >= 18 OBSERVATIONS
keep if num_hrly_obs >= 18

drop *gmt

notes: NO2 monitors in Chicago CBSA
notes: Keep only monitor-days with 18 or more hourly observations
notes: Generated by extract_chicago_no2_2000_2012.do
compress
save "$data/chicago_no2_2000_2012_daily.dta", replace

erase "$data/temp.dta"
erase "$data/temp2.dta"

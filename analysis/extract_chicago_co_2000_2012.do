global rawdata C:\Users\fanga\Desktop\Econ Materials\STATA\replications in stata\AirPollu-criminal-rep\Raw-Data
global data C:\Users\fanga\Desktop\Econ Materials\STATA\replications in stata\AirPollu-criminal-rep\Data
clear all
*this set up will be permanently changed in stata
set mem 1000m
set matsize 2000

//Step1: Append Data//
insheet using "$rawdata/co_chicago_20000101_20041231.txt", comma clear
save "$data/temp.dta", replace
insheet using "$rawdata/co_chicago_20050101_20091231.txt", comma clear
save "$data/temp2.dta", replace
insheet using "$rawdata/co_chicago_20100101_20121231.txt", comma clear
append using "$data/temp.dta"
append using "$data/temp2.dta"

drop if latitude == "END OF FILE"
destring latitude longtitude, replace

gen str monitorid = string(county)+"_"+string(sitenum)+"_"+string(poc)

//Step2: Generate Hourly Dataset//
** 8 HOUR RUNNING AVERAGE IS SOMETHING I CAN RE-CALCULATE MYSELF;
* THERE ARE ALMOST NO OBSERVATIONS WITH AN 8 HOUR AND NO 1 HOUR;
preserve

keep if sampleduration=="1 HOUR"
gen observed = !mi(samplemeasurement)
bys monitorid datelocal: egen num_hrly_obs = sum(observed)

* KEEP DAYS WITH >= 18 OBSERVATIONS;
keep if num_hrly_obs >= 18

*generate variable for date and hour
gen date = date(datelocal,"YMD",2015) //When a two-digit year is encountered, the largest year, topyear, that does not exceed Y isreturned.
format date %td

* substr uses by programmers who want to extract a subset of bytes from a string
gen hour = substr(hourlocal,1,2)
destring hour, replace

rename samplemeasurement co_hrly
lab var co_hrly "Hourly Concentration of CO, ppm"

keep latitude longitude datum statecode countycode monitorid sitenum poc co_hrly date hour
compress

notes: CO monitors in Chicago CBSA
notes: Keep only monitor-days with 18 or more hourly observations
notes: Generated by extract_chicago_co_2000_2012.do
save "$data/chicago_co_2000_2012_hourly.dta", replace

restore

//Step3: Generate Daily Dataset//
gen observed = !mi(samplemeasurement)
keep if sampleduration == "1 HOUR"

bysort monitorid datelocal sampleduration: egen num_hrly_obs = sum(observed)

bys monitorid datelocal sampleduration: egen max_co = max(samplemeasurement)
bys monitorid datelocal sampleduration: egen mean_co = mean(samplemeasurement)

lab var max_co "Day's max hourly concentration CO, ppm"
lab var mean_co "Day's avg hourly concentration CO, ppm"

*generate variable for date and hour
gen date = date(datelocal,"YMD",2015) 
format date %td

*drop obs with missing value
*It tags just one observation in each distinct group defined by varlist
egen tag = tag(monitorid date)
keep if tag==1
drop tag

* KEEP DAYS WITH >= 18 OBSERVATIONS;
keep if num_hrly_obs >= 18

drop *gmt

notes: CO monitors in Chicago CBSA
notes: Keep only monitor-days with 18 or more hourly observations
notes: Generated by extract_chicago_co_2000_2012.do
compress
save "$data/chicago_co_2000_2012_daily.dta", replace

erase "$data/temp.dta"
erase "$data/temp2.dta"

